apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0"
    useLibrary 'org.apache.http.legacy'

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    defaultConfig {
        applicationId "com.vuze.android.remote"
        minSdkVersion 7
        targetSdkVersion 23
        versionCode 76
        versionName "2.5.0"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            debuggable true
            versionNameSuffix ' Alpha'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-debug.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt', 'proguard-project-release.txt'
            debuggable false
        }
    }
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    flavorDimensions "coreD", "gaD"

    productFlavors {
        coreFlavor {
            dimension "coreD"
        }

        noCoreFlavor {
            dimension "coreD"
        }

        gaFlavor {
            dimension "gaD"
        }

        nogaFlavor {
            dimension "gaD"
        }
    }

    sourceSets {
        coreFlavor.java.srcDirs = ['src/coreFlavor/java']
        noCoreFlavor.java.srcDirs = ['src/noCoreFlavor/java']
        gaFlavor.java.srcDirs = ['src/gaFlavor/java']
        nogaFlavor.java.srcDirs = ['src/nogaFlavor/java']
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}


dependencies {
    compile 'com.android.support:support-annotations:24.1.1'
    compile project(':pagerSlidingTabStrip')
    compile project(':recyclerview-fastscroll')
    compile project(':android-numberpicker')
    gaFlavorCompile 'com.google.android.gms:play-services-analytics:+'
    coreFlavorCompile files('libs/vuzeandroidcore.jar')
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:leanback-v17:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:recyclerview-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.alibaba:fastjson:1.1.46.android'
    compile 'jcifs:jcifs:1.3.17'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'divstar:ico4a:v1.0'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.google.android.gms:play-services-analytics:9.4.0'
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += "--set-max-idx-number=53000" // default 60000
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.getkeepsafe.dexcount'
